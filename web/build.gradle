buildscript {
	ext {
		springBootVersion = '2.7.17'
	}
    repositories {

        jcenter()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/snapshot" }
        mavenCentral() 
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'war'
    id 'org.springframework.boot' version '2.7.17'
/*
 * The dependency management plugin remains a transitive dependency of spring-boot-gradle-plugin
 * so thereâ€™s no need for it to be listed as a classpath dependency in the buildscript configuration.
 */
    id 'io.spring.dependency-management' version '1.1.3'
    id 'maven-publish'
}

// Fix for log4j vulnerability, remove when updating spring boot version
ext['log4j2.version'] = '2.17.1'

def bomVersion = "2021.0.7"

group = "gov.nih.nci.term.browser.web"
version = "1.10.0.RELEASE"

sourceCompatibility = 17
targetCompatibility = 17

springBoot {
    buildInfo()          // generate file META-INF/build-info.properties during the build
}

// Java Toolchain to detect which version of Java to use, if not present
// it will automatically download and install the required version of Java
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven { url "https://repo.spring.io/release" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/snapshot" }
    mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-actuator'
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.cloud:spring-cloud-dependencies:2021.0.7'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway:3.1.7'
    implementation 'org.springframework.boot:spring-boot-properties-migrator'

    implementation 'io.projectreactor:reactor-core'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.13.1'
}

jar {
    archiveBaseName = project.name
    version =  project.version
     manifest {
        attributes 'Main-Class': 'main.java.gov.nih.evsexplore.web.Application'
    }
}

jar {
    from('frontend/dist') {
        //Public is a default supported Spring Boot resources directory.
        into 'public'
    }
}
 
//frontend:build will be run before the processResources
processResources.dependsOn('frontend:build')

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$bomVersion"
    }
}

test {
    /*
     * Configure gradle to show standard out and standard error
     * of the test JVM(s) on the console
     */
    testLogging.showStandardStreams = true

    
}

tasks.register('zipFile', Zip) {
    def zipFileRootDirectory = "${project.name}-${project.version}"

    from('src/main/bin') {
        fileMode = 0755
        into "$zipFileRootDirectory/bin"
    }
    from("$buildDir/libs") {
        include '*.war'
        into "$zipFileRootDirectory/lib"
    }
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}


zipFile.dependsOn = [ bootWar ]
build.dependsOn = [ test, bootWar, zipFile ]
publishToMavenLocal.dependsOn = [ bootWar, zipFile ]


